// Used for both the 'aar' file and publish.
def VERSION_NAME = '0.1.0-SNAPSHOT'
def PACKAGE = 'is.hello.go99'

buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'com.jakewharton.sdkmanager:gradle-plugin:0.12.0'
    }
}

// <http://stackoverflow.com/questions/29663918/android-gradle-javadoc-annotation-does-not-exists>
configurations {
    javadocDeps
}

apply plugin: 'com.android.library'
apply plugin: 'maven-publish'

android {
    compileSdkVersion 22
    buildToolsVersion "22.0.1"

    defaultConfig {
        minSdkVersion 14
        targetSdkVersion 22
        versionCode 1
        versionName VERSION_NAME

        testApplicationId "${PACKAGE}.tests"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile 'com.android.support:support-annotations:22.2.0'
    javadocDeps 'com.android.support:support-annotations:22.2.0'

    testCompile "org.robolectric:robolectric:3.0"
    testCompile 'junit:junit:4.12'
    testCompile 'org.mockito:mockito-core:1.10.19'
}

// See README.md for deploy instructions.
//
// Based on information from the following sources:
// <http://mike-neck.github.io/blog/2013/06/21/how-to-publish-artifacts-with-gradle-maven-publish-plugin-version-1-dot-6>
// <http://stackoverflow.com/questions/26874498/maven-publish-android-library-with-aar-and-source-jar>
task sourceJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier "source"
}

task javadoc(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
    classpath += configurations.javadocDeps
    options.links("http://docs.oracle.com/javase/7/docs/api/");
    exclude '**/BuildConfig.java'
    exclude '**/R.java'
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives javadocJar
    archives sourceJar
}

publishing {
    repositories {
        maven {
            if (VERSION_NAME.endsWith('-SNAPSHOT')) {
                url ""
            } else {
                url ""
            }
            credentials(AwsCredentials) {
                accessKey ""
                secretKey ""
            }
        }

        publications {
            mavenJava(MavenPublication) {
                groupId = PACKAGE
                artifactId = 'anime'
                version = VERSION_NAME

                artifact(sourceJar)
                artifact(javadocJar)
                artifact("${project.buildDir}/outputs/aar/${project.name}-release.aar")

                // The publication doesn't know about our dependencies,
                // so we have to manually add them to the pom
                pom.withXml {
                    asNode().children().last() + {
                        resolveStrategy = DELEGATE_FIRST
                        name 'anime-android-go-99'
                        description 'A small library to make animations on Android less annoying.'
                        url 'https://github.com/hello/anime-android-go-99'
                        scm {
                            url 'https://github.com/hello/anime-android-go-99'
                            connection 'scm:git:git@github.com:hello/anime-android-go-99.git'
                            developerConnection 'scm:git:git@github.com:hello/anime-android-go-99.git'
                        }
                        licenses {
                            license {
                                name 'The Apache Software License, Version 2.0'
                                url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                                distribution 'repo'
                            }
                        }
                        developers {
                            developer {
                                id 'decarbonization'
                                name 'Kevin MacWhinnie'
                                email 'km@sayhello.com'
                            }
                        }
                    }

                    def dependenciesNode = asNode().appendNode('dependencies')
                    configurations.compile.allDependencies.each {
                        // Skip the libs rule
                        if(it.group != null) {
                            def dependencyNode = dependenciesNode.appendNode('dependency')
                            dependencyNode.appendNode('groupId', it.group)
                            dependencyNode.appendNode('artifactId', it.name)
                            dependencyNode.appendNode('version', it.version)
                        }
                    }
                }
            }
        }
    }
}

